# Copyright 2025 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

"""Repository rule for local PyTorch development."""

def _create_shadow_tree(rctx, torch_path):
    """Creates a shadow tree of symlinks to the PyTorch source directory."""

    # Source Directory Mode (Shadow Tree)
    # We create a shadow tree so we can safely delete PyTorch's internal BUILD files.
    # List of directories to symlink from source to the Bazel repository root. Including 'tools'
    # and 'cmake' is essential as many PyTorch internal files load .bzl files from those roots.
    dirs_to_link = [
        "c10",
        "aten",
        "torch",
        "torchgen",
        "include",
        "build",
        "tools",
        "cmake",
        "functorch",
    ]
    for d in dirs_to_link:
        src_dir = "{source}/{dir}".format(source = torch_path, dir = d)

        # Only symlink if the directory exists in the source
        if rctx.path(src_dir).exists:
            dest_dir = "site-packages/{dir}".format(dir = d)

            # Ensure the destination directory exists before copying.
            rctx.execute(["mkdir", "-p", dest_dir])

            # cp -as creates a shadow tree:
            # it mirrors the directory structure (real directories) but symlinks all files.
            # TODO(ecalubaquib): Remove GNU specific cp command with starlark portable solution.
            rctx.execute(["cp", "-as", src_dir + "/.", dest_dir + "/"])

    # Link specific third-party subdirectories needed for PyTorch.
    # By default, we link common dependencies, but users can override this in the
    # repository rule attributes to avoid conflicts (e.g. with absl or protobuf).
    for d in rctx.attr.third_party_dirs:
        src_dir = "{source}/third_party/{dir}".format(source = torch_path, dir = d)
        if rctx.path(src_dir).exists:
            dest_dir = "site-packages/third_party/{dir}".format(dir = d)
            rctx.execute(["mkdir", "-p", dest_dir])
            rctx.execute(["cp", "-as", src_dir + "/.", dest_dir + "/"])

            # Break the documented infinite symlink loop in ittapi if it exists.
            if d == "ittapi":
                rctx.execute(["rm", "-rf", "site-packages/third_party/ittapi/rust/ittapi-sys/c-library"])

    # Special handling for libraries in 'torch/lib' or 'build/lib'
    # We ensure 'torch/lib' exists and symlink all found .so files.
    rctx.execute([rctx.path(rctx.attr._link_libraries_sh), torch_path, "site-packages"])

def _torch_local_repo_impl(rctx):
    # We use TORCH_SOURCE to specify the path (e.g., /home/user/pytorch or <path_to_pytorch_wheel>.whl)
    torch_path = rctx.getenv("TORCH_SOURCE", "")

    if not torch_path:
        # Fallback for when the env var isn't set (e.g. non-local analysis)
        rctx.file("BUILD.bazel", """
load("@rules_cc//cc:cc_library.bzl", "cc_library")
package(default_visibility = ["//visibility:public"])
cc_library(name = "torch_headers")
cc_library(name = "libc10")
cc_library(name = "libtorch_cpu")
cc_library(name = "libtorch_python")
cc_library(name = "torch_libs")
cc_library(name = "torch")
""")
        return

    # Handle Wheel Extraction (.whl) vs Directory Symlinking
    if torch_path.endswith(".whl"):
        # Bazel 7.x doesn't support the 'type = "whl"' argument in rctx.extract,
        # and it strictly checks extensions. We symlink to .zip internally.
        rctx.symlink(torch_path, "_torch_internal_archive.zip")
        rctx.execute(["mkdir", "-p", "site-packages"])
        rctx.extract("_torch_internal_archive.zip", output = "site-packages")
    else:
        _create_shadow_tree(rctx, torch_path)

    # Early Validation: Ensure we actually found the core libraries.
    # This prevents obscure "missing input file" errors later in the build.
    if not rctx.path("site-packages/torch/lib/libc10.so").exists:
        fail("\n" + "=" * 80 +
             "\nERROR: Could not find PyTorch libraries (libc10.so) in provided TORCH_SOURCE." +
             "\n" +
             "\nPath searched: {path}".format(path = torch_path) +
             "\n" +
             "\nThis usually happens if:" +
             "\n1. TORCH_SOURCE points to the wrong directory." +
             "\n2. PyTorch has not been built yet" +
             "\n    (run 'python setup.py develop' or 'build' in the pytorch repo)." +
             "\n3. The build artifacts are in an unexpected location." +
             "\n" + "=" * 80 + "\n")

    # Remove the conflicting BUILD files. Since we are using our own root BUILD file,
    # we must hide PyTorch's internal BUILD files so Bazel doesn't treat subdirectories
    # as separate packages.
    rctx.execute(["find", "site-packages", "(", "-name", "BUILD", "-o", "-name", "BUILD.bazel", ")", "-delete"])

    # Create the dynamic BUILD file.
    # If TORCH_SOURCE is a directory, _create_shadow_tree might have symlinked a 'torch/include'
    # which usually indicates an installed layout (site-packages) rather than a source repo.
    is_source = not torch_path.endswith(".whl") and not rctx.path("site-packages/torch/include").exists
    rctx.file(
        "BUILD.bazel",
        content = """# Generated by torch_local_repo
load("{defs}", "define_torch_local")

package(default_visibility = ["//visibility:public"])

define_torch_local(name = "torch_local", is_source_mode = {is_source})
""".format(
            defs = str(rctx.attr._torch_local_defs),
            is_source = "True" if is_source else "False",
        ),
    )

torch_local_repo = repository_rule(
    implementation = _torch_local_repo_impl,
    environ = ["TORCH_SOURCE"],  # Triggers rebuild if this var changes
    attrs = {
        "third_party_dirs": attr.string_list(
            default = ["fmt", "onnx", "pybind11", "kineto", "ittapi"],
            doc = "Subdirectories inside third_party/ to symlink. " +
                  "Selective to avoid absl/protobuf conflicts.",
        ),
        "_torch_local_defs": attr.label(
            default = ":torch_local_defs.bzl",
            allow_single_file = True,
        ),
        "_link_libraries_sh": attr.label(
            default = ":link_libraries.sh",
            allow_single_file = True,
        ),
    },
    local = True,  # Critical: Re-run this rule if local files change
)
